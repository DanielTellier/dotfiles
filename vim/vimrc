let $RTP = split(&runtimepath,',')[0]
let $VRC = $RTP.'/vimrc'
let $SDIR = $RTP.'/sessions'
let g:fugitive_git_executable = '/usr/bin/git'
let g:snippets_base_directory = $RTP.'/plugin/vim-snippets/snippets'
let g:netrw_banner = 0
let g:netrw_liststyle = 3
let g:netrw_browse_split = 4
let g:netrw_preview = 1
let g:netrw_winsize = 80
let g:rehash256 = 1

colorscheme molokai
filetype plugin indent on
syntax on

if system('uname -s') == "Darwin\n"
  set clipboard=unnamed " Mac
else
  set clipboard=unnamedplus " Linux
endif

set autoindent autoread
set backspace=start,eol,indent
set backupdir=$HOME/.local/share/vim/backup/
set colorcolumn=80
highlight ColorColumn ctermbg=22
set complete=.,w,b,u,t
set noconfirm
set encoding=utf-8
set expandtab tabstop=4 softtabstop=4 shiftwidth=4
set fillchars=vert:\|,fold:-
set formatoptions=tcqj
set hidden
set history=10000
set hlsearch incsearch
set ignorecase smartcase
set laststatus=2
set list
set listchars=tab:>-,precedes:.,trail:.,extends:.,eol:$
set nocompatible
set nojoinspaces
set nostartofline
set noswapfile
set nowrap
set number relativenumber
set redrawtime=10000 " Loading syntax in files
set ruler
set runtimepath+=$HOME/.vim/after
set viminfo+=n~/.viminfo
set scrolloff=8
set showcmd
set sidescroll=1
set sidescrolloff=8
set smarttab
set splitright
set statusline=%02n:%<%f\ %h%m%r%=%-14.(%l,%c%V%)\ %P
set t_Co=256
set title
set ttyfast
set undolevels=100
set undodir=$HOME/.local/state/vim/undo/
set undofile
set updatetime=5000 " Time in milliseconds for CursorHold to take effect
set wildignore=*.o,*.d,*.exe,*.a,*.so,*.out
set wildmenu
set wildmode=longest:full,full
set writebackup

function! ToggleNetrw()
    exe '1tabnext'
    if &ft !=# "netrw"
        exe '0tabnew'
        exe 'Explore'
    else
        exe 'tabclose'
    endif
endfunction

function! SplitNetrw(bufcmd, is_stay)
    exe 'normal v'
    let l:bufcmd = a:bufcmd
    let bufn = bufname()
    exe 'close!'
    if tabpagenr('$') == 1
        exe 'tabnew'
        let l:bufcmd = 'edit'
    else
        exe '2tabnext'
    endif

    exe l:bufcmd . ' ' . bufn

    " If true move back to netrw on first tab
    if a:is_stay
        exe '1tabnext'
    endif
endfunction

function! ToggleAll()
    if &number || &relativenumber || &list
        set nonumber norelativenumber nolist
    else
        set number relativenumber list
    endif
endfunction

function! ToggleNumbers()
  if &relativenumber
    set norelativenumber
  elseif &number
    set nonumber
  else
    set number relativenumber
  endif
endfunction

function! SurroundMappings(map_type)
    let chars = ['`', "'", '"', ']', '}', ')', '>', 't']
    for char in chars
        if a:map_type == "word"
            exe 'nmap <leader>w'.char.' ysiw'.char
        elseif a:map_type == "line"
            exe 'nmap <leader>l'.char.' yss'.char
        elseif a:map_type == "change"
            for curr_char in chars
                if curr_char == char
                    continue
                endif
                exe 'nmap <leader>'.curr_char.char.' cs'.curr_char.char
            endfor
        elseif a:map_type == "delete"
            exe 'nmap <leader>d'.char.' ds'.char
        elseif a:map_type == "visual"
            exe 'vmap <leader>v'.char.' S'.char
        endif
    endfor
endfunction

function! MkSession()
    let sfile = $RTP."/sessions/".substitute(getcwd()[1:].".vim", "/", "-", "g")
    " Close netrw before saving session
    exe '1tabnext'
    if &ft ==# "netrw"
        exe 'tabclose'
    endif
    exe 'mksession! '.sfile
endfunction

function! SoSession()
    let sfile = $RTP."/sessions/".substitute(getcwd()[1:].".vim", "/", "-", "g")
    if filereadable(sfile)
        exe 'source '.sfile
        highlight ColorColumn ctermbg=22
    else
        echo "File does not exist ".sfile
    endif
endfunction

command! -nargs=+ -complete=file Grep call search#Grep_(<f-args>)

augroup spellcheck
  autocmd!
  autocmd FileType gitcommit,markdown setlocal spell spelllang=en_us
  autocmd FileType gitcommit,markdown setlocal complete+=kspell
  autocmd FileType tex,latex,context,plaintex setlocal spell spelllang=en_us
  autocmd FileType tex,latex,context,plaintex setlocal complete+=kspell
augroup end

" File templates
augroup templates
  autocmd!
  autocmd BufNewFile *.py 0r $RTP/templates/template.py
augroup END

augroup autosave
  autocmd!
  autocmd CursorHold,CursorHoldI,QuitPre * :wa
augroup END

augroup netrw
    autocmd!
    " Hack fix to make ctrl-l work properly
    autocmd FileType netrw noremap <buffer> <c-l> <c-w>l
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>e :call SplitNetrw("edit", 0)<cr>
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>o :call SplitNetrw("split", 0)<cr>
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>v :call SplitNetrw("vsplit", 0)<cr>
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>E :call SplitNetrw("edit", 1)<cr>
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>O :call SplitNetrw("split", 1)<cr>
    autocmd FileType netrw nnoremap <buffer> <silent> <nowait> <leader>V :call SplitNetrw("vsplit", 1)<cr>
augroup END

let mapleader="\<space>"

" Move cursor infront of closing pair symbol
let g:AutoPairsShortcutJump = "<c-p>"

" Quit vim
noremap <silent> <leader>q :qa!<cr>

" Make session file for cwd
noremap <silent> <leader>sm :call MkSession()<cr>

" Source session file at cwd
noremap <silent> <leader>ss :call SoSession()<cr>

" Open key mappings doc
noremap <silent> <leader>hd :tabnew $RTP/doc/common-maps.txt<cr>

" Open vimrc
noremap <silent> <leader>hv :tabnew $RTP/vimrc<cr>

" Toggle highlights
nnoremap <silent> <leader>th
\ :if (&hls && v:hlsearch) \| nohlsearch \| else \| set hlsearch \| endif<cr>

" Toggle listchars and numbers
nnoremap <silent> <leader>ta :call ToggleAll()<cr>

" Toggle listchars
nnoremap <silent> <leader>tl
\ :if (&list) \| set nolist \| else \| set list \| endif<cr>

" Toggle numbers
nnoremap <silent> <leader>tn :call ToggleNumbers()<cr>

" Toggle netrw
nnoremap <silent> <leader>tw :call ToggleNetrw()<cr>

" Global substitution
nnoremap <leader>rg :%s/<c-r><c-w>//g<left><left>
nnoremap <leader>rp :%s/<c-r><c-w>//gc<left><left><left>
vnoremap <leader>rg "zy:%s/<c-r>z//g<left><left>
vnoremap <leader>rp "zy:%s/<c-r>z//gc<left><left><left>

" Search word under cursor
nnoremap <silent> <leader>gw :Grep . all "\b<c-r><c-w>\b"<cr>:cw<cr>

" Search word user input
nnoremap <leader>gi :Grep<space>

" paste last thing yanked, not deleted
nmap ,p "0p
nmap ,P "0P

" Terminal access
nnoremap <silent> <leader>me :terminal<cr>
nnoremap <silent> <leader>mv :vertical terminal<cr>
nnoremap <silent> <leader>mh :horizontal terminal<cr>
nnoremap <silent> <leader>mt :tab terminal<cr>
tnoremap <silent> <leader>mc <c-w>:hide<cr>
tnoremap <silent> <leader>mq <c-w>:quit!<cr>

" Terminal modes
tnoremap <silent> <esc> <c-w>n

" Terminal window movement
tnoremap <c-h> <c-w><c-w>h
tnoremap <c-j> <c-w><c-w>j
tnoremap <c-k> <c-w><c-w>k
tnoremap <c-l> <c-w><c-w>l

" You Surrounds word
call SurroundMappings("word")

" You Surrounds line
call SurroundMappings("line")

" Change Surrounds
call SurroundMappings("change")

" Delete Surrounds
call SurroundMappings("delete")

" Visual Surrounds
call SurroundMappings("visual")

" Compiler make
nnoremap <silent> <leader>cm :make % <bar> redraw!<cr><cr>

" Quick Fix List next and previous
noremap <silent> <leader>fn :cn<cr>
noremap <silent> <leader>fp :cp<cr>

" Make Y behave like other capitals
nnoremap Y y$

" Keep it centered
nnoremap n nzzzv
nnoremap N Nzzzv
nnoremap J mzJ`z

" Command line mode without shift+;
nnoremap ; :
vnoremap ; :

" Faster split window navigation
nnoremap <c-h> <c-w>h
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l

" Force use of hjkl-style movement and up(c-b)/down(c-f)
map <up> <nop>
map <down> <nop>
map <left> <nop>
map <right> <nop>
map <pageup> <nop>
map <pagedown> <nop>
map <home> <nop>
map <end> <nop>
imap <up> <nop>
imap <down> <nop>
imap <left> <nop>
imap <right> <nop>
imap <pageup> <nop>
imap <pagedown> <nop>
imap <home> <nop>
imap <end> <nop>

" Remap these keys to work with hjkl-style movement
map $ <nop>
map ^ <nop>
map { <nop>
map } <nop>
noremap K {
noremap J }
noremap H ^
noremap L $

" Tabbing
nnoremap <tab> >>
nnoremap <s-tab> <<
vnoremap <tab> >>
vnoremap <s-tab> <<

" Allow saving of files as sudo when I forgot to start vim using sudo.
" cmap w!! w !sudo tee > /dev/null %
